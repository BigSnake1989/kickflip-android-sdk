apply plugin: 'com.android.library'

def getVersionCode() {
    return 9
}

def getVersionName() {
    return '1.2'
}

android {
    compileSdkVersion 19
    buildToolsVersion "20.0"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 21
        versionCode getVersionCode()
        versionName getVersionName()

    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt', 'proguard-google-api-client.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

configurations {
    jaxDoclet
    classpaths
}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile 'com.android.support:support-v4:21.0.3'

    // google-oauth-java-client
    compile files('libs/google-http-client-1.17.0-rc.jar')
    compile files('libs/google-http-client-android-1.17.0-rc.jar')
    compile files('libs/google-http-client-jackson2-1.17.0-rc.jar')
    compile files('libs/google-oauth-client-1.17.0-rc.jar')
    compile files('libs/gson-2.1.jar')
    compile files('libs/jsr305-1.3.9.jar')
    compile files('libs/protobuf-java-2.4.1.jar')

    compile 'com.amazonaws:aws-android-sdk-core:2.1.5'
    compile 'com.amazonaws:aws-android-sdk-s3:2.1.5'

    compile 'com.fasterxml.jackson.core:jackson-core:2.5.0'
    // fulfilled by aws-android-sdk:
    //compile files('libs/jackson-core-2.1.3.jar')
    //compile files('libs/jackson-core-asl-1.9.11.jar')

    // m3u8 parser
    compile files('libs/m3u8parser-0.2.jar')

    // For Doclava JavaDoc
    jaxDoclet("com.google.doclava:doclava:1.0.5")
    //classpaths files('build/classes/release')
    classpaths fileTree(dir: 'build/classes/release').matching { include 'io/kickflip/sdk/Kickflip.java' }
    classpaths files(new File(System.getenv('ANDROID_HOME') + '/platforms/android-21/android.jar'))
}

// Maven publish task
// Uncomment this line to enable publishing to Maven Central. Requires maven
// credentials in gradle.properties
//apply from: 'https://raw.github.com/OnlyInAmerica/mvn-repo/master/maven.gradle'

// Generate JavaDoc
android.libraryVariants.all { variant ->

    task("generate${variant.name}Javadoc", type: Javadoc) {
        source = android.sourceSets.main.java
        title = null
        options.docletpath = configurations.jaxDoclet.files.asType(List)
        options.classpath = configurations.classpaths.files.asType(List)
        options {
            doclet "com.google.doclava.Doclava"
            bootClasspath new File(System.getenv('JAVA_HOME') + "/jre/lib/rt.jar")
            addStringOption "warning", '110'  // Allow undocumented types
            addStringOption "overview", "/Users/davidbrodsky/Code/android/kickflip-android-example/submodules/kickflip-android-sdk/sdk/src/javadoc-overview.html"
            addStringOption "public"
            addStringOption "hdf project.name", "Kickflip Android SDK"
            addStringOption "federate android", "http://d.android.com/reference"
            addStringOption "federationxml android", "http://doclava.googlecode.com/svn/static/api/android-10.xml"
        }
    }

}
